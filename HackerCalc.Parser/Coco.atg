
$namespace=HisRoyalRedness.com

using System.IO;

COMPILER Coco

/*-------------------------------------------------------------------------*/

CHARACTERS
	letter          = "ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz_".
	digit           = "0123456789".
	cr              = '\r'.
	lf              = '\n'.
	tab             = '\t'.
	stringCh        = ANY - '"' - '\\' - cr - lf.
	charCh          = ANY - '\'' - '\\' - cr - lf.
	hex             = "0123456789abcdefABCDEF".
    
    /* Ordered from tightest to loosest binding order */
    logNotOp        = '!'.
    bitNotOp        = '~'.
    multOp          = '*'.
    divideOp        = "/\\".
    moduloOp        = '%'.
    addOp           = '+'.
    subOp           = '-'.
    shiftLeftOp     = '<'.
    shiftRightOp    = '>'.
    andOp           = '&'.
    orOp            = '|'.
    xorOp           = '^'.

TOKENS
	integer         = digit { digit }. 
    hex_integer     = "0x" hex { hex }.
    float           = {digit} '.' digit {digit}.

    notToken        = logNotOp | bitNotOp.
    multToken       = multOp | divideOp | moduloOp.
    addToken        = addOp | subOp.
    shiftToken      = shiftLeftOp shiftLeftOp | shiftRightOp shiftRightOp.
    bitToken        = andOp | orOp | xorOp.


IGNORE cr + lf + tab

/*-------------------------------------------------------------------------*/

PRODUCTIONS

Coco
= Expr.

Expr                = BitExpr.

BitExpr
=                   
                    ShiftExpr
                    {
                        bitToken            (. AddBitOperator(t.val); .)
                        ShiftExpr
                    }
.

ShiftExpr
=                   AddExpr
                    {
                        shiftToken          (. AddShiftOperator(t.val); .)
                        AddExpr
                    }
.

AddExpr 
=                   MulExpr 
                    { 
                        addToken            (. AddAddOperator(t.val); .)
                        MulExpr 
                    }
.

MulExpr
=                   NotExpr
                    {
                        multToken           (. AddMultOperator(t.val); .)
                        NotExpr
                    }
.

NotExpr
=                   Term
                    |
                        notToken            (. AddNotOperator(t.val); .)
                        Term
.

Term 
=                                           (. var intLen = ""; .)
                    (
                        "("                 (. AddLeftBracket(); .)
                        Expr 
                        ")"                 (. AddRightBracket(); .)
                        | integer           (. AddInteger(t.val, intLen); .)
                        | hex_integer       (. AddInteger(t.val, intLen); .)
                        | float             (. AddFloat(t.val); .)
                    )
.



/*
Term 
=                                           (. var intValue = ""; .)
                                            (. var intLen = ""; .)
                    integer                 (. intValue = t.val; .)
                    [
                       '_' ( "4" | "8" | "16" | "32" | "64" | "128" | "256" )          (. intLen = t.val; .)
                    ]
                                            (. AddInteger(intValue, intLen); .)
                    | float                 (. AddFloat(t.val); .)
.
*/


END Coco.
